# 批处理汇总

## 1、txt文本按行合并批处理
::: tip 说明
**“SrcFile=a.txt”** —— 原始txt文件 

**“DstFile=b.txt”** —— 输出txt文件名 

**“MergeNum=5”**  —— 合并的行数。如果是5就写5。其他请替换
:::


```batch
@echo off
title set “SrcFile=a.txt”，原始txt文件 set “DstFile=b.txt”  ，输出txt文件名 set “MergeNum=5”  ，合并的行数。如果是5就写5。其他请替换。
setlocal enabledelayedexpansion
set "SrcFile=a.txt"
set "DstFile=b.txt"
set "MergeNum=5"
set "Delimiter=;"
 
type nul>"%DstFile%"
for /f "usebackq tokens=*" %%a in ("%SrcFile%") do (
    set /a RowTotal+=1
    set /a Count+=1
    set "RowMerge=!RowMerge!%Delimiter%%%a"
    if !Count! equ !MergeNum! (
        >>"%DstFile%" echo,!RowMerge:~1!
        set RowMerge=
        set Count=0
    )
)
 
set /a RowMod=RowTotal%%MergeNum
if !RowMod! equ 0 (
    goto :end
)
>>"%DstFile%" echo,!RowMerge:~1!
:end
```

## 2、按行拆分批处理
::: tip 说明
将一个多行文件拆分成多个一行一个的文件 

例如：文件a有100行内容，运行批处理后得到文件1.txt - 100.txt
:::
```batch
@echo off&setlocal enabledelayedexpansion
for /r %%a in (*.txt) do (
   set num=0
   for /f "usebackq delims=" %%b in ("%%~nxa") do (
      set /a num+=1
      echo.%%b>"%%~na-!num!.txt"
   )
)
pause
```
## 3、穿插合并文件批处理
::: tip 说明
将两个文件进行 行内穿插得到新的文件

例如：文件a内容为1，3，5 - 文件b内容为2，4，6 ,运行批处理得到文件c，内容为1，2，3，4，5，6
:::
```batch
@Echo Off
title 交叉合并文本内容 

:GTBegin
@echo 默认合并目录下 a.txt 和 b.txt 两个文本文件到 c.txt，确定继续？(y/n)
set /p GTConfirm=
if "%GTConfirm%"=="y" goto GTYES
if "%GTConfirm%"=="Y" goto GTYES
if "%GTConfirm%"=="n" goto GTNO
if "%GTConfirm%"=="N" goto GTNO
echo 请输入正确的指令
pause >nul
goto GTBegin

:GTYES
pushd %cd%
for /f "delims=" %%a in ('dir /a-d /b *.txt') do (
findstr /r /c:"[^ ]" "%%a">#
move # "%%a"
)
popd
For /F "tokens=1* delims=:" %%i In ('Findstr /n .* b.txt') Do Set "_%%i=%%j"
(For /F "delims=" %%i In ('Findstr /n .* a.txt') Do (
  Set "Str=%%i"& Set /A Count+=1
  SetLocal EnableDelayedExpansion
  For %%j In (!Count!) Do (
    Echo,!Str:*:=!
    Echo,!_%%j!
    )
  EndLocal
))>c.txt
Start c.txt
echo. :) 搞定咯
pause >nul
exit

:GTNO
exit
```

## 4、去重、去空行、并合并所有文件
::: tip 说明
将文件内容去重，去空行，并合并当前文件夹内的所有文件得到一个新汇总文件
:::
```batch
@echo off
title 合并文本删除重复行 

:GTBegin
@echo 默认合并目录下所有文本文件到 all.txt，确定继续？(y/n)
set /p GTConfirm=
if "%GTConfirm%"=="y" goto GTYES
if "%GTConfirm%"=="Y" goto GTYES
if "%GTConfirm%"=="n" goto GTNO
if "%GTConfirm%"=="N" goto GTNO
echo 请输入正确的指令
pause >nul
goto GTBegin

:GTYES
for /f "delims=" %%a in ('dir /a-d /b /on *.txt') do (
  for /f "delims=" %%b in ('type "%%a"') do (
    if not defined %%b set %%b=Def & >>$ echo,%%b
  )
)
move $ "all.txt"
echo. :) 搞定
pause >nul
exit

:GTNO
exit
```


## 5、随机批量重命名批处理
::: tip 说明
将文件夹内的文件随机命名，并自动排序

例如：文件内有a,b,c三个文件，每次运行批处理可得到不同文件的命名文件并排序，可用做【随机功能】

a - 1 or a - 2
:::
```batch
@echo off&setlocal EnableDelayedExpansion
set n=0
for /f "delims=" %%i in ('dir /b *.jpg') do ren "%%i" "R%%~ni%%~xi"
for /f "delims=" %%i in ('dir /b *.jpg') do set /a n+=1
for /f "delims=" %%i in ('dir /b *.jpg') do (
call:RR
ren "%%i" "!R!%%~xi"
)
exit
:RR
set /a R=%random%%%n%+1
if !R!==!W%R%! Goto:RR
set /a W!R!=!R!
Goto:eof
```